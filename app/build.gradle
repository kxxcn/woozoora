plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-android-extensions'
    id 'androidx.navigation.safeargs.kotlin'
    id 'com.google.firebase.crashlytics'
}

android {
    compileSdkVersion 30
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "dev.kxxcn.woozoora"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 10
        versionName "1.0.9"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            manifestPlaceholders = [crashlyticsCollectionEnabled: "false"]
            minifyEnabled false
        }
        release {
            manifestPlaceholders = [crashlyticsCollectionEnabled: "true"]
            minifyEnabled true
            proguardFile getDefaultProguardFile('proguard-android-optimize.txt')
            proguardFile 'proguard-rules.pro'
            proguardFile 'proguard-firebase.pro'
            proguardFile 'proguard-retrofit.pro'
            proguardFile 'proguard-gson.pro'
            proguardFile 'proguard-glide.pro'
            proguardFile 'proguard-mpandroidchart.pro'
        }
    }

    flavorDimensions "api"

    productFlavors {
        def URL = "CONNECTED"

        develop {
            dimension "api"
            buildConfigField "String", URL, '"http://172.30.1.24:8000/v1/"'
        }
        product {
            dimension "api"
            buildConfigField "String", URL, '"http://3.36.8.193/v1/"'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xmaxerrs" << "500"
        }
    }

    dataBinding {
        enabled = true
    }

    androidExtensions {
        experimental = true
    }
}

dependencies {
    def androidx_core_version = "1.3.2"
    def androidx_appcompat_version = "1.2.0"
    def androidx_constraint_version = "2.0.4"
    def material_version = "1.2.1"
    def lifecycle_version = "2.2.0"
    def firebase_version = "26.3.0"
    def play_services_auth_version = "19.0.0"
    def dagger_version = "2.31"
    def dagger_assisted_version = "0.6.0"
    def retrofit_version = "2.9.0"
    def interceptor_version = "4.9.0"
    def kakao_version = "2.2.0"
    def room_version = "2.2.6"
    def glide_version = "4.11.0"
    def chart_version = "v3.1.0"
    def lottie_version = "3.6.0"
    def circular_progress_version = "0.1.2"
    def bottom_bar_version = "1.0.9"
    def spark_version = "1.2.0"
    def skeleton_version = "4.0.0"
    def arch_testing_version = "2.1.0"
    def coroutines_version = "1.3.6"
    def truth_version = "0.44"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "androidx.core:core-ktx:$androidx_core_version"
    implementation "androidx.appcompat:appcompat:$androidx_appcompat_version"
    implementation "androidx.constraintlayout:constraintlayout:$androidx_constraint_version"
    implementation "com.google.android.material:material:$material_version"

    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    implementation platform("com.google.firebase:firebase-bom:$firebase_version")
    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.google.firebase:firebase-dynamic-links-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'

    implementation "com.google.android.gms:play-services-auth:$play_services_auth_version"

    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"
    implementation "com.squareup.inject:assisted-inject-annotations-dagger2:$dagger_assisted_version"
    kapt "com.squareup.inject:assisted-inject-processor-dagger2:$dagger_assisted_version"

    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$interceptor_version"

    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    implementation 'com.orhanobut:logger:2.2.0'

    implementation "com.kakao.sdk:v2-user:$kakao_version"

    implementation "com.github.PhilJay:MPAndroidChart:$chart_version"

    implementation "com.airbnb.android:lottie:$lottie_version"

    implementation "com.github.VaibhavLakhera:Circular-Progress-View:$circular_progress_version"
    implementation "nl.joery.animatedbottombar:library:$bottom_bar_version"
    implementation "com.robinhood.spark:spark:$spark_version"
    implementation "com.faltenreich:skeletonlayout:$skeleton_version"

    testImplementation 'junit:junit:4.+'
    testImplementation "androidx.room:room-testing:$room_version"
    testImplementation "androidx.arch.core:core-testing:$arch_testing_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
    testImplementation "com.google.truth:truth:$truth_version"

    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}
